
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java-library-distribution'

group = 'com.siftscience'
version = '3.22.0'

repositories {
    mavenCentral()
}

compileJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

compileTestJava {
  sourceCompatibility = 1.8
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '5.1.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '3.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'
    implementation 'com.squareup.okio:okio:3.16.4'
    implementation 'commons-codec:commons-codec:1.19.0'
}

buildscript {
    repositories {
        jcenter()
    }
}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javaDocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

artifacts {
    archives jar
    archives sourceJar
    archives javaDocJar
}

signing {
    required {
        System.getenv('CIRCLE_BRANCH') == null
    }
    sign configurations.archives
}

def pomConfig = {
    name "${project.name}"
    url 'https://github.com/SiftScience/sift-java'
    packaging 'jar'
    licenses {
        license {
            name 'MIT License'
            url 'https://github.com/SiftScience/sift-java/blob/master/LICENSE'
            distribution 'repo'
        }
    }
    inceptionYear 2016
    organization {
        name 'Sift Science'
        url 'https://sift.com'
    }
    developers {
        developer {
            id 'sift-api-platform'
            name 'Sift API Platform Team'
        }
    }
    scm {
        url 'https://github.com/SiftScience/sift-java'
        connection 'scm:git:https://github.com/SiftScience/sift-java.git'
        developerConnection 'scm:git:ssh://git@github.com/SiftScience/sift-java.git'
    }
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java

            artifact(sourceJar) {
                classifier = "sources"
            }

            artifact(javaDocJar) {
                classifier = "javadoc"
            }

            pom.packaging = "jar"

            pom.withXml {
                configurations.runtimeClasspath.resolvedConfiguration.firstLevelModuleDependencies.each {
                    dep ->
                        asNode().dependencies[0].dependency.find {
                            it.artifactId[0].text() == dep.moduleName &&
                                    it.groupId[0].text() == dep.moduleGroup
                        }?.scope[0]?.value = 'compile'
                }
            }

            pom.withXml {
                def root = asNode()
                def last = root.children().last()
                last.parent().appendNode('description', 'Sift Science Java API')
                last + pomConfig
            }
        }
    }

    repositories {
        maven {
            name 'staging'
            url = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'

            credentials {
                username = "${sonatypeUsername}"
                password = "${sonatypePassword}"
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.jar
}

distributions {
    main {
        distributionBaseName = 'sift-java'
    }
}
