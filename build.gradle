buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java-library-distribution'

group = 'com.siftscience'
version = '3.21.2'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileTestJava {
    sourceCompatibility = 1.8
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '5.1.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '3.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'
    implementation 'com.squareup.okio:okio:3.15.0'
    implementation 'commons-codec:commons-codec:1.19.0'
}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javaDocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

artifacts {
    archives jar
    archives sourceJar
    archives javaDocJar
}

signing {
    required {
        System.getenv('CIRCLE_BRANCH') == null
    }
    sign configurations.archives
}

def pomConfig = {
    name "${project.name}"
    url 'https://github.com/SiftScience/sift-java'
    packaging 'jar'
    licenses {
        license {
            name 'MIT License'
            url 'https://github.com/SiftScience/sift-java/blob/master/LICENSE'
            distribution 'repo'
        }
    }
    inceptionYear 2016
    organization {
        name 'Sift Science'
        url 'https://siftscience.com'
    }
    developers {
        developer {
            id 'lopatin'
            name 'Alex Lopatin'
            url 'https://github.com/lopatin'
        }
    }
    scm {
        url 'https://github.com/SiftScience/sift-java'
        connection 'scm:git:https://github.com/SiftScience/sift-java.git'
        developerConnection 'scm:git:ssh://git@github.com/SiftScience/sift-java.git'
    }
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java

            artifact(sourceJar) {
                classifier = "sources"
            }

            artifact(javaDocJar) {
                classifier = "javadoc"
            }

            pom.packaging = "jar"

            pom.withXml {
                configurations.runtimeClasspath.resolvedConfiguration.firstLevelModuleDependencies.each {
                    dep ->
                        asNode().dependencies[0].dependency.find {
                            it.artifactId[0].text() == dep.moduleName &&
                                    it.groupId[0].text() == dep.moduleGroup
                        }?.scope[0]?.value = 'compile'
                }
            }

            pom.withXml {
                def root = asNode()
                def last = root.children().last()
                last.parent().appendNode('description', 'Sift Science Java API')
                last + pomConfig
            }

            // Sign the pom.xml.
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
                pomFile.delete()
            }

            // Sign the artifacts.
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    def zipMatcher = it.file =~ /\.(zip|tar)\.asc$/
                    def extensionPrefix = 'jar'
                    if (zipMatcher.find()) {
                        extensionPrefix = zipMatcher.group(1)
                    }
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }

                    extension = extensionPrefix + '.asc'
                }
            }
        }
    }
//    repositories {
//        maven {
//            name 'staging'
//            url = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
//
//            credentials {
//                username = "${sonatypeUsername}"
//                password = "${sonatypePassword}"
//            }
//        }
//    }
}

nexusPublishing {
    repositories {
        // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
        sonatype {
//             nexusUrl = uri("https://your-server.com/staging")
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
//            username = "${sonatypeUsername}" // defaults to project.properties["myNexusUsername"]
//            password = "${sonatypePassword}" // defaults to project.properties["myNexusPassword"]
        }
    }
}



model {
    tasks.publishJarPublicationToStagingRepository {
        dependsOn(project.tasks.signArchives)
    }
}

distributions {
    main {
        distributionBaseName = 'sift-java'
    }
}
